COMMENT = _{single_line_comment}
single_line_comment = { "#" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{" " | "\t"}

keyword = _{^"InterfaceVersion" | ^"Model" | ^"CorrectionType" | ^"IRregularisation" | ^"MassiveParticleScheme"
            | ^"CouplingPower" | ^"MassiveParticles" | ^"DebugUnstable" | ^"Extra" | ^"extra" | ^"ewscheme"
            | ^"ewrenormalisationscheme" | ^"alphapower" | ^"alphaspower"}

config_line = _{keyword ~ (!"|" ~ ANY)* ~ "|" ~ ("OK" | "Ok" | "ok") ~ NEWLINE}


amplitude_type = {"Tree" | "Loop" | "ccTree" | "scTree" | "scTree2" | "LoopInduced"}

pdg_number = ${"-"? ~ (!" " ~ ASCII_DIGIT)+}

initial_list = {pdg_number+}

final_list = {pdg_number+}

process_spec = { initial_list ~ "->" ~ final_list }

process_id = {ASCII_DIGIT*}

amplitude_type_specification = { "AmplitudeType" ~ amplitude_type ~ "|" ~ ("OK" | "Ok" | "ok")}

subprocess = { process_spec ~ "|" ~ "1" ~ process_id }

file = {
    SOI ~ NEWLINE* ~
    ((subprocess | amplitude_type_specification | config_line) ~ NEWLINE*)* ~ NEWLINE* ~
    EOI
}